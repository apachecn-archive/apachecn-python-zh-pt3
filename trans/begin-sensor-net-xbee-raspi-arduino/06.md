# 6.基于 Arduino 的传感器节点

物理计算的最大进步之一是微控制器的激增。微控制器由一个处理器、一个小指令集、存储器和可编程输入/输出电路组成，包含在单个芯片上。微控制器通常与支持电路和连接一起封装在一个小的印刷电路板上。

微控制器用于嵌入式系统中，可以定制小型软件程序来控制和监控硬件设备，使其成为传感器网络的理想选择。Arduino 平台是最成功和最受欢迎的微控制器之一。

在本章中，您将探索 Arduino 平台，目标是使用 Arduino 管理传感器节点。您会看到一个关于 Arduino 的简短教程和几个帮助您开始使用 Arduino 的项目。

## 什么是 Arduino？

Arduino 是一个由开源软件环境支持的开源硬件原型平台。它于 2005 年首次推出，设计目标是使硬件和软件易于使用，并尽可能提供给最广泛的受众。因此，使用 Arduino 并不需要成为电子专家。

最初的目标受众包括艺术家和爱好者，他们需要一个微控制器来使他们的设计和创作更有趣。然而，由于其易用性和多功能性，Arduino 很快成为更广泛的受众和更广泛的项目的选择。

这意味着您可以将 Arduino 用于各种项目，从对环境条件做出反应到控制复杂的机器人功能。Arduino 还通过实际应用使学习电子学变得更加容易。

帮助 Arduino 平台快速采用的另一个方面是通过 Arduino 官方网站( [`http://arduino.cc/en/`](http://arduino.cc/en/) )提供的丰富信息的贡献者社区不断增长。当您访问该网站时，您会发现一个优秀的“入门”教程，以及一个有用的项目想法列表和一个完整的类似 C 语言的参考指南，用于编写控制 Arduino 的代码(称为草图)。

Arduino 还提供了一个名为 Arduino IDE 的集成开发环境。IDE 在您的计算机(称为主机)上运行，在那里您可以编写和编译草图，然后通过 USB 连接将它们上传到 Arduino。IDE 可用于 Linux、Mac 和 Windows。它是围绕一个专门为编写代码而设计的文本编辑器和一组支持编译和加载草图的有限功能而设计的。

草图是以一种特殊的格式编写的，只包含两个必需的方法——一个在 Arduino 复位或通电时执行，另一个持续执行。因此，您的初始化代码放在`setup()`中，控制 Arduino 的代码放在`loop()`中。这种语言类似于 C 语言，你可以定义自己的变量和函数。关于写草图的完整指南，见 [`http://arduino.cc/en/Tutorial/Sketch`](http://arduino.cc/en/Tutorial/Sketch) 。

您可以通过编写封装了某些功能(如联网、使用存储卡、连接数据库、做数学等)的库来扩展草图的功能并提供重用。

Arduino 支持许多模拟和数字引脚，您可以使用这些引脚来连接各种设备和组件并与之交互。主流主板有特定的引脚布局或接头，允许使用称为屏蔽的扩展板。Shields 允许您为 Arduino 添加额外的硬件功能，如以太网、蓝牙和 XBee 支持。Arduino 和盾牌的物理布局允许你堆叠盾牌。因此，您可以拥有以太网屏蔽和 XBee 屏蔽，因为两者使用不同的 I/O 引脚。在探索 Arduino 在传感器网络中的应用时，您将学习插针和屏蔽的使用。

接下来的部分将研究各种 Arduino 板，并简要描述它们的功能。我按上市时间列出这些主板，从最新型号开始。有更多的电路板和变体可供使用，在本书出版时可能会有一些新的电路板问世，但这些都是传感器网络项目中通常使用的电路板。

### Arduino 型号

越来越多的 Arduino 板可供使用。一些是为特殊应用而配置的，而另一些是为不同的处理器和内存配置而设计的。一些主板被认为是官方的 Arduino 主板，因为它们是由 Arduino.cc 标记和认可的。由于 Arduino 是使用知识共享署名共享许可进行许可的，任何人只要遵守许可，都可以构建 Arduino 兼容的主板。本节研究一些更受欢迎的 Arduino 品牌主板。

Arduino 板的基本布局包括一个 USB 连接、一个电源连接器、一个复位开关、用于电源和串行通信的 led 以及一组用于连接屏蔽的标准间隔接头。官方电路板采用独特的蓝色印刷电路板，上面印有白色字样。除了一个型号之外，所有官方主板都可以安装在一个机箱中(它们在 PCB 上有孔，用于安装螺钉)。例外情况是 Arduino 设计用于安装在试验板上。

#### 优诺牌

Uno 板是大多数 Arduino 新手都会选择的标准 Arduino 板。它采用了 ATmega328P 处理器；14 个数字 I/O 引脚，其中 6 个可作为脉宽调制 <sup>[1](#Fn1)</sup> 输出；和 6 个模拟输入引脚。Uno 板有 32KB 的闪存和 2KB 的 SRAM。

Uno 有表面贴装器件(SMD)和标准 IC 插座两种形式。如果您希望使用外部 IC 编程器来构建定制解决方案，IC 插座版本允许您更换处理器。详情和完整的数据表可在 [`https://store.arduino.cc/usa/arduino-uno-rev3`](https://store.arduino.cc/usa/arduino-uno-rev3) 获得。它有一个标准的 USB 型连接器，支持所有屏蔽。图 [6-1](#Fig1) 显示了 Arduino Uno 板卡。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig1_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig1_HTML.jpg)

图 6-1

Arduino Uno Rev3(由 Arduino.cc 提供)

还有一种版本的主板内置 Wi-Fi 芯片，可以用于传感器网络或使用 Wi-Fi 屏蔽有问题的情况(缺乏空间，与其他屏蔽冲突等)。).虽然名称相同，但它在几个方面与标准的 Uno 不同。除了 Wi-Fi 芯片，它还有一个不同的处理器和一个更少的 PWM 引脚。你可以在 [`https://store.arduino.cc/usa/arduino-uno-WiFi-rev2`](https://store.arduino.cc/usa/arduino-uno-WiFi-rev2) 了解更多关于 Uno Wi-Fi 板的信息。图 [6-2](#Fig2) 显示了 Arduino Uno Wi-Fi 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig2_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig2_HTML.jpg)

图 6-2

Arduino Uno Wi-Fi 版本 2(由 Arduino.cc 提供)

#### 【男性名字】利奥纳多

Leonardo 板代表 Arduino 平台中的另一种标准板。略有不同的是，虽然它支持标准接头布局，但它还有一个 USB 控制器，允许电路板作为 USB 设备(如鼠标或键盘)出现在主机上。该板使用较新的 ATmega32u4 处理器，具有 20 个数字 I/O 引脚，其中 12 个可用作模拟引脚，7 个可用作脉宽调制(PWM)输出。它有 32KB 的闪存和 2.5KB 的 SRAM。

莱昂纳多比乌诺有更多的数字引脚，但继续支持大多数盾牌。USB 连接使用较小的 USB 连接器。该板有带接头和不带接头两种。图 [6-3](#Fig3) 描绘了一个官方的莱昂纳多董事会。详情和完整的数据表可在 [`https://store.arduino.cc/usa/leonardo`](https://store.arduino.cc/usa/leonardo) 找到。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig3_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig3_HTML.jpg)

图 6-3

Arduino Leonardo(由 Arduino.cc 提供)

#### 由于

Arduino Due 是一款基于 Atmel SAM3X8E ARM Cortex-M3 处理器的新型、更大、更快的主板。处理器为 32 位处理器，板卡支持海量的 54 个数字 I/O 端口，其中 14 个可用于 PWM 输出；12 路模拟输入；4 个 UART 芯片(串行端口)以及 2 个数模转换器(DAC)和 2 个双线接口(TWI)引脚。新处理器有几个优点:

*   32 位寄存器

*   DMA 控制器(允许独立于 CPU 的内存任务)

*   512KB 闪存

*   96KB SRAM

*   84 兆赫时钟

Due 具有更大的外形尺寸(称为 mega footprint ),但仍然支持使用标准屏蔽和 mega 格式屏蔽。这种新板有一个明显的限制:不像其他板在 I/O 引脚上可以接受高达 5V 的电压，在 I/O 引脚上的 Due 限制为 3.3V。详情和完整的数据表可在 [`https://store.arduino.cc/usa/due`](https://store.arduino.cc/usa/due) 找到。

Arduino Due 旨在用于需要更多处理能力、更多内存和更多 I/O 引脚的项目。尽管新板的功能非常强大，但它仍然是开源的，价格与以前的板相当。寻找您的项目需要最大硬件性能的原因。图 [6-4](#Fig4) 显示了一个 Arduino Due 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig4_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig4_HTML.jpg)

图 6-4

arduino Due(arduino . cc 提供)

#### Mega 2560

Arduino Mega 2560 是 Due 的旧版本。它基于 ATmega2560 处理器(因此得名)。与 Due 一样，该板支持大量的 54 个数字 I/O 端口，其中 14 个可用作 PWM 输出、16 个模拟输入和 4 个 UARTs(硬件串行端口)。它使用 16MHz 时钟和 256KB 闪存。详情和完整的数据表可在 [`https://store.arduino.cc/usa/mega-2560-r3`](https://store.arduino.cc/usa/mega-2560-r3) 找到。

Mega 2560 本质上是标准 Arduino Uno 和 Leonardo 的更大形式，但支持标准盾牌(以及“Mega”盾牌)。图 [6-5](#Fig5) 显示了 Arduino Mega 2560 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig5_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig5_HTML.jpg)

图 6-5

Arduino Mega(由 Arduino.cc 提供)

有趣的是，Arduino Mega 256 是 Prusa Mendel 和类似 3D 打印机的首选主板，这些打印机需要使用名为 rep rap Arduino Mega polo Lu Shield(RAMPS)的控制板。

Tip

请注意 Due 比 Uno 大多少。如果您选择安装 Due、Mega 或类似的电路板，您可能需要留出更多空间来安装电路板。

#### 微处理器

Arduino Micro 是 Leonardo 板的一种特殊形式，使用带有 20 个数字 I/O 引脚的相同处理器，其中 12 个可用作模拟引脚，7 个可用作 PWM 输出。它有 32KB 的闪存和 2.5KB 的 SRAM。详情和完整的数据表可在 [`https://store.arduino.cc/usa/arduino-micro`](https://store.arduino.cc/usa/arduino-micro) 找到。

这款 Micro 与 Mini 一样，是为在试验板上使用而设计的，但形式更新颖、更新。但与 Mini 不同的是，Micro 是一个全功能的主板，配有 USB 连接器。和 Leonardo 一样，它有内置的 USB 通信，允许电路板作为鼠标或键盘连接到计算机。图 [6-6](#Fig6) 显示了 Arduino 微板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig6_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig6_HTML.jpg)

图 6-6

Arduino Micro(由 Arduino.cc 提供)

虽然品牌是官方的 Arduino 板，但 Arduino Micro 是与 Adafruit 合作生产的。

#### NANOTECHNOLOGY 简称

Arduino Nano 是 Arduino Micro 的旧版本。在这种情况下，它基于 Duemilanove4 的功能，具有 ATmega328 处理器(旧型号使用 ATmega168)和 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，8 个可用作模拟输入。mini 有 32KB 的闪存，使用 16MHz 时钟。详情和完整的数据表可在 [`https://store.arduino.cc/usa/arduino-nano`](https://store.arduino.cc/usa/arduino-nano) 找到。

像 Micro 一样，它具有通过 USB 连接进行连接和编程所需的所有功能。图 [6-7](#Fig7) 显示了一个 Arduino 纳米板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig7_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig7_HTML.jpg)

图 6-7

Arduino Nano(由 Arduino.cc 提供)

#### MKR 系列主板

Arduino 还有另一种形式，叫做 MKR(意为“制造者”)系列。MKR 系列包括基于(现已退役的 Zero)板的各种板，具有各种通信功能，如 Wi-Fi、LoRa、LoRaWAN 和 GSM。

它们基于 Atmel ATSAMW25 SoC(片上系统),专为物联网项目和设备而设计。它还支持加密认证。对于那些在需要电池端口的项目上工作的人来说，MKR 系列电路板包括一个 LiPo 充电电路，用于在使用外部电源运行时为 LiPo 电池充电。详情和完整的数据表可在 [`https://store.arduino.cc/usa/arduino-mkr1000`](https://store.arduino.cc/usa/arduino-mkr1000) 找到。

这些板不使用与 Uno 兼容的基于屏蔽的板相同的引脚布局(但是你可以得到一个适配器)。相反，它们的设计类似于 Nano 和 Mini(但稍大一点),以最小化电路板的尺寸，从而更容易集成到您的项目中。事实上，它们是物联网(IoT)项目的首选板之一，是传感器网络项目的绝佳选择。由于它们相对较新，并且有些具有专门的通信选项，大多数 Arduino 新手最好从支持 Uno 兼容屏蔽的 Arduino 板开始。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig8_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig8_HTML.jpg)

图 6-8

mkr 1000(arduino . cc 提供)

Caution

MKR 板采用 3.3V 电源供电，GPIO 引脚上的最大输入为 3.3V

### arduino 翻制

越来越多的 Arduino 板可从大量来源获得。因为 Arduino 是开放的硬件，所以发现世界各地的厂商生产的 Arduino 板并不罕见，甚至有点不合法。

虽然有些人会坚持认为真正的 Arduinos 是那些品牌的，但事实是，只要构建质量良好，组件质量高，选择使用品牌还是副本，也就是克隆，是个人偏好之一。我已经从许多来源对 Arduino 板进行了采样，除了少数例外，它们都出色地执行了预期的功能。

除了 Arduino Mini，Arduino 克隆板的硬件配置种类更多。有些 Arduinos 是为嵌入式系统或试验板设计的，有些是为原型设计的。在接下来的几节中，我将研究一些更流行的克隆板。

#### 迷你版 Arduino

Arduino Pro Mini 是 SparkFun 的另一款主板。它基于 ATmega168 处理器(旧型号使用 ATmega168 ),有 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，8 个模拟输入。Pro Mini 有 16KB 的闪存和 1KB 的 SRAM，它使用 16MHz 的时钟。详情和完整的数据表可在 [`www.sparkfun.com/products/11113`](http://www.sparkfun.com/products/11113) 找到。

Arduino Pro Mini 以 Arduino Mini 为模型，也用于试验板，但不带有接头。这使得 Arduino Pro Mini 成为半永久性安装的理想选择，在这种安装中，引脚可以焊接到组件或电路上，空间非常宝贵。图 [6-9](#Fig9) 显示了一个 Arduino Pro 迷你板。它真的那么小。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig9_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig9_HTML.jpg)

图 6-9

Arduino Pro Mini(由 SparkFun 提供)

此外，Pro Mini 不包括 USB 连接器，因此必须连接到 FTDI 电缆或类似的分线板并进行编程。它有两种型号，一种是带 8MHz 时钟的 3.3V 型号，另一种是带 16MHz 时钟的 5V 型号。

#### 菲欧

Arduino Fio 是 SparkFun 制作的另一款主板。它是为无线项目设计的。它基于 ATmega32U4 处理器，具有 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，8 个模拟引脚。详情和完整的数据表可在 [`www.sparkfun.com/products/11520`](http://www.sparkfun.com/products/11520) 找到。

Fio 需要一个 3.3V 电源，允许使用锂聚合物(LiPo)电池，该电池可以通过板上的 USB 连接器充电。

在主板底部的 XBee 插座中可以看到它的无线血统。虽然 USB 连接可以让您给电池充电，但您必须使用 FTDI 电缆或分线适配器来连接和编程 Fio。与 Pro 型号类似，Fio 不带有接头，允许电路板用于半永久性安装，连接焊接到位。图 [6-10](#Fig10) 显示了一个 Arduino Fio 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig10_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig10_HTML.jpg)

图 6-10

Arduino Fio(由 SparkFun 提供)

#### 西迪耶诺

Seeeduino 是 Seeed Studio ( [`www.seeedstudio.com`](http://www.seeedstudio.com) )制作的 Arduino 克隆体。它基于 ATmega328P 处理器，具有 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，8 个模拟引脚。它有 32KB 的闪存和 2KB 的 SRAM。详情和完整的数据表可在 [`www.seeedstudio.com/Seeeduino-V4-2-p-2517.html`](http://www.seeedstudio.com/Seeeduino-V4-2-p-2517.html) 找到。

该板的尺寸与 Arduino Uno 相似，支持所有标准接头。它支持许多增强功能，如 I2C 和串行 Grove 连接器和迷你 USB 连接器，并使用 SMD 组件。这也是一个醒目的红色黄色标题。图 [6-11](#Fig11) 显示了一个 Seeeduino 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig11_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig11_HTML.jpg)

图 6-11

Seeeduino(由 Seeed Studio 提供)

Seeed Studio 还制作了该板的“迷你”版本()。

#### 啜

SpikenzieLabs ( [`www.spikenzielabs.com`](http://www.spikenzielabs.com) )的 Sippino 设计用于无焊料试验板。它的成本更低，因为它的元件更少，占地面积更小。它是未组装的，如果你正在学习焊接，可以成为一个非常愉快的下午项目。

它基于 ATmega328 处理器，有 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，6 个模拟输入引脚。Sippino 板有 32KB 的闪存和 2KB 的 SRAM。详情和完整的数据表可在 [`www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/sippino-kit?cPath=1&`](http://www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/sippino-kit%253FcPath%253D1%2526) 找到。

Sippino 没有 USB 连接，因此您必须使用 FTDI 电缆对其进行编程。好消息是，无论您的项目中有多少个 Sippinos，您都只需要一根电缆。我有许多 Sippinos，并在我的许多 Arduino 项目中使用它们，这些项目的空间非常宝贵。图 [6-12](#Fig12) 显示了安装在试验板上的 Sippino。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig12_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig12_HTML.jpg)

图 6-12

西皮诺(SpikenzieLabs 提供)

SpikenzieLabs 还做了一个带 USB 接口的版本( [`www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/sippino8482-usb-kit?cPath=1&`](http://www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/sippino8482-usb-kit%253FcPath%253D1%2526) )。

虽然您不能在 si Pino 上使用普通屏蔽，但 SpikenzieLabs 还提供了一种称为屏蔽坞的特殊适配器，允许您将 si Pino 与标准 Arduino 屏蔽配合使用。shield dock 是一个令人惊叹的附件，它可以让您像使用标准的 Uno 或 Duemilanove 一样使用 Sippino。图 [6-13](#Fig13) 显示了安装在屏蔽坞上的西皮诺。详情和完整的数据表可在 [`www.spikenzielabs.com/Catalog/spikenzielabs/the-shield-dock`](http://www.spikenzielabs.com/Catalog/spikenzielabs/the-shield-dock) 找到。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig13_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig13_HTML.jpg)

图 6-13

盾牌码头上的西皮诺(SpikenzieLabs 提供)

#### 原型

Prototino 是 SpikenzieLabs 的另一个产品。它具有与 Sippino 相同的元件，但它不是一个友好的试验板布局，而是安装在一个包含完整原型制作区域的 PCB 上。与 Sippino 一样，它基于 ATmega328 处理器，具有 14 个数字 I/O 引脚，其中 6 个可用作 PWM 输出，6 个模拟输入引脚。Prototino 板有 32KB 的闪存和 2KB 的 SRAM。详情和完整的数据表可在 [`www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/prototino?cPath=1&`](http://www.spikenzielabs.com/Catalog/arduino/sippino-prototino-8482/prototino%253FcPath%253D1%2526) 找到。

Prototino 是构建具有支持组件和电路的解决方案的理想选择。在某些方面，它类似于 Nano、Mini 和类似的板，因为您可以将其用于永久安装。但与这些电路板(甚至 Arduino Pro)不同的是，Prototino 为您提供了一个直接将组件添加到电路板的空间。我在项目中使用了许多 Prototino 板，在这些项目中，我将组件添加到 Prototino 并将其安装在机箱中。这让我可以使用一块板创建一个解决方案，甚至可以快速轻松地构建几个副本。

像 Sippino 一样，Prototino 没有 USB 连接，所以你必须使用 FTDI 电缆对其进行编程。图 [6-14](#Fig14) 显示了一个 Prototino 板。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig14_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig14_HTML.jpg)

图 6-14

Prototino(由 SpikenzieLabs 提供)

#### 阿达福罗地铁站

Adafruit 的 Metro 是一组 Arduino 兼容板，支持多种格式，包括几种支持 Arduino 屏蔽的格式。我喜欢的版本是 Metro 328 ( [`www.adafruit.com/product/2488`](http://www.adafruit.com/product/2488) )。图 [6-15](#Fig15) 显示了 Adafruit 的 Metro 328 板。该板使用 16MHz 的 ATmega328P 和许多小的改进，使 Metro 成为 Arduino Uno 的优秀替代产品。查看产品页面了解更多详情。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig15_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig15_HTML.jpg)

图 6-15

地铁 328 号线(由 Adafruit 提供)

Note

Adafruit 也有几个 CircuitPython 模型。有些更小，因此如果您需要最小化节点的占用空间，这可能是一个选项。详见 [`www.adafruit.com/category/966`](http://www.adafruit.com/category/966) 。

There is Even One Made of Paper

时不时地，你会遇到一些平凡的事情，因为媒介的改变而变得非常有趣。古伊列梅·马丁斯创立的 PAPERduino 就是这样一个例子。PAPERduino 是一个最小的 Arduino，它使用纸质模板代替 PCB。您只需下载并打印模板，购买一份常用分立元件清单，然后按照模板上打印的连接图将元件焊接到短导线上。您可以访问以下网站了解更多信息: [`http://lab.guilhermemartins.net/2009/05/06/paperduino-prints/`](http://lab.guilhermemartins.net/2009/05/06/paperduino-prints/) 。

### 那么，我该买哪个呢？

如果你想知道该买哪个 Arduino，答案取决于你想做什么。对于本书中的大多数项目，任何 Arduino Uno 或支持标准 shield headers 的类似克隆都可以。您不需要购买更大的 Due 或其前身，因为不需要增加内存和 I/O 引脚。

我使用 Arduino Uno、Uno Wi-Fi 或 Leonardo 来完成本书中的所有项目。虽然您可以使用旧的板没有问题，但使用莱昂纳多板有一些问题。当你遇到这些问题时，我会指出来。大多数问题都与列奥纳多板上重新定位的图钉有关。例如，在 Leonardo 上，SPI 接头引脚(在图 [6-3](#Fig3) 的左上角)已经被移动。

对于未来的项目，在选择 Arduino 之前，您应该考虑一些事情。例如，如果您的项目主要基于试验板，或者您想要将项目的物理大小保持在最小，并且您不打算使用任何屏蔽，Arduino Mini 可能是更好的选择。相反，如果您计划进行大量的编程来实现复杂的数据操作或分析算法，您可能需要考虑它所增加的处理能力和内存。

底线是，大多数情况下，您的选择将基于物理特性(大小、屏蔽支持等)，很少基于处理能力或内存。SparkFun 有一个优秀的买家指南，在其中你可以看到每种选择的利弊。详见 [`www.sparkfun.com/pages/arduino_guide`](http://www.sparkfun.com/pages/arduino_guide) 。

### 去哪里买

由于 Arduino 平台的流行，许多供应商出售 Arduino 和 Arduino 克隆板、屏蔽和附件。Arduino.cc 网站( [`https://store.arduino.cc/usa`](https://store.arduino.cc/usa) )也有一个专门介绍授权经销商的页面。如果这里列出的资源都不适合你，你可以在这个页面上找到你附近的零售商。

#### 在线零售商

有越来越多的在线零售商，你可以在那里购买 Arduino 板和配件。下面列出了一些比较受欢迎的网站:

*   *SparkFun* :从分立元件到公司自有品牌的 Arduino 克隆和盾牌，SparkFun 几乎拥有你可能想要的任何 Arduino 平台( [`www.sparkfun.com/`](http://www.sparkfun.com/) `)`)。

*   *阿达果*:携带越来越多的组件、小工具等等。它为电子爱好者提供越来越多的产品，包括 Arduino 产品的完整系列。Adafruit 还拥有出色的文档库和 wiki 来支持其销售的所有产品( [`www.adafruit.com/`](http://www.adafruit.com/) `)`)。

你也可以拜访一些克隆板的制造商。以下是领先的克隆制造商及其店面链接:

*   *SpikenzieLabs*:[`www.spikenzielabs.com/`](http://www.spikenzielabs.com/)

*   *Seeed 工作室* : [`www.seeedstudio.com/`](http://www.seeedstudio.com/)

#### 零售店(美国)

也有实体商店销售 Arduino 产品。虽然没有在线零售商那么多，而且他们的库存通常也有限，但是如果你很快需要一个新的 Arduino 板，你可以在以下零售商那里找到。你可能会在你所在的地区找到更多的零售商。寻找受欢迎的业余电子商店:

*   *Fry's* :一家电子超市，有一个巨大的仓库，里面存放着电子产品、组件、微控制器、电脑零件等，可供订购。Fry's 出售 Arduino 品牌的电路板、盾牌和配件，以及 Parallax、SparkFun 和更多`(` [`http://frys.com/`](http://frys.com/) `).`的产品

*   *微中心*:微中心类似于 Fry 的，提供大量的产品库存。然而，大多数微型中心商店的电子元件库存比弗莱氏( [`www.microcenter.com/`](http://www.microcenter.com/) `).`)要少

现在，您已经对硬件细节和各种可用的 Arduino 板有了更好的了解，让我们深入了解如何使用和编程 Arduino。下一节提供安装 Arduino 编程环境和 Arduino 编程的教程。后面的章节将介绍一些项目，以培养你开发传感器网络的技能。

## Arduino 教程

本节是一个简短的教程，介绍如何开始使用 Arduino。它涵盖了获取和安装 IDE 以及编写示例草图。我没有重复本书之前的优秀作品，而是涵盖了重点，并向不太熟悉 Arduino 的读者推荐在线资源和其他提供更深入介绍的书籍。此外，Arduino IDE 有许多示例草图，您可以使用它们自己探索 Arduino。大多数在 Arduino.cc 网站上都有相应的教程。

### 学习资源

有很多关于 Arduino 平台的信息。如果您刚刚开始使用 Arduino，Apress 提供了一系列令人印象深刻的书籍，涵盖了与 Arduino 相关的各种主题，从开始使用微控制器到学习其设计和实现的细节。以下是一些比较受欢迎的书籍。有些比你想象的要老一些，但是仍然非常有用。

*   迈克尔·麦克罗伯茨的《Arduino 入门》

*   *实用 Arduino:开源硬件的酷项目(技术在行动)*作者 Jonathan Oxer 和 Hugh bl mings(a press，2009)

*   戴尔·麦特(2011 年出版)的《Arduino 内部原理》

还有一些优秀的在线资源，可以帮助您了解更多关于 Arduino、Arduino 库和示例项目的信息。以下是一些最好的:

*   *Arduino.cc* : [`http://arduino.cc/en/`](http://arduino.cc/en/)

*   *阿达果* : [`http://learn.adafruit.com/`](http://learn.adafruit.com/)

*   *SparkFun* : [`https://learn.sparkfun.com/`](https://learn.sparkfun.com/)

### Arduino IDE

Arduino IDE 可供 Mac、Linux (32 位和 64 位版本)和 Windows 平台下载。你可以从 [`http://arduino.cc/en/Main/Software`](http://arduino.cc/en/Main/Software) 下载 IDE。每个平台都有链接，如果需要为不同的平台编译 IDE，还可以链接到源代码。

Tip

有趣的是，有一个网页版的 IDE，你可以不用安装在电脑上就可以使用。如果您想在不想(或不能)安装 IDE 的 PC 上使用它，这可能会有所帮助。

安装 IDE 非常简单。为了简洁起见，我省略了安装 IDE 的实际步骤，但是如果您需要安装 IDE 的演示，您可以在下载页面上看到入门链接，或者阅读 Michael McRoberts 的*开始 Arduino* 中的更多内容(Apress，2010)。

IDE 启动后，您会看到一个简单的界面，其中包含一个文本编辑器区域(默认为白色背景)，编辑器下方的一个消息区域(默认为黑色背景)，以及顶部的一个简单的*按钮栏。按钮(从左至右)*编译*、*编译上传*、*新建*、*打开*、*保存*。右边还有一个打开串行监视器的按钮。您可以使用串行监视器查看通过串行库发送(或打印)的 Arduino 消息。你可以在你的第一个项目中看到这一点。图 [6-16](#Fig16) 显示了 Arduino IDE。*

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig16_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig16_HTML.jpg)

图 6-16

Arduino IDE

注意，在图 [6-16](#Fig16) 中，你可以看到一个样本草图(称为`blink`)和一个成功编译操作的结果。我通过点击*文件* ➤ *例子* ➤ *基本* ➤ *眨眼*来加载这个草图。注意底部，它告诉你你正在一个特定的串行端口上编程一个 Arduino Leonardo 板。

由于处理器和支持架构的不同，编译器构建程序的方式(以及 IDE 上传程序的方式)也有所不同。因此，当您启动 IDE 时，首先要做的事情之一就是从*工具* ➤ *电路板*菜单中选择您的电路板。图 [6-17](#Fig17) 显示了在 Mac 上选择板卡的示例。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig17_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig17_HTML.jpg)

图 6-17

选择 Arduino 板

请注意可用的电路板数量。请务必选择与您的主板相匹配的产品。如果您使用的是克隆板，请查看制造商网站，了解推荐使用的设置。如果你选择了错误的板，你通常会在上传时得到一个错误，但是你选择了错误的板可能并不明显。因为我有很多不同的主板，所以我养成了每次启动 IDE 时都选择主板的习惯。

接下来您需要做的是选择 Arduino 板连接的串行端口。要连接到板上，使用*工具* ➤ *端口*菜单选项。图 [6-18](#Fig18) 显示了 Mac 上的一个例子。在这种情况下，没有列出串行端口。如果您没有将 Arduino 插入电脑的 USB 端口(或集线器),您已经将它插入但在某个时候将其断开，或者您没有加载 Arduino 的驱动程序(Windows ),就会发生这种情况。通常，这可以通过简单地拔出 Arduino 并将其插回，然后等待直到计算机识别该端口来解决。

Note

如果你用的是 Mac，选择哪个端口并不重要:无论是以`tty`开头的端口还是以`cu`开头的端口都可以。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig18_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig18_HTML.jpg)

图 6-18

选择串行端口

Tip

如果您需要在 Windows 上安装驱动程序的帮助，请参见 [`www.arduino.cc/en/Guide/HomePage?from=Guide.Howto`](http://www.arduino.cc/en/Guide/HomePage%253Ffrom%253DGuide.Howto) 。

好了，现在你已经安装了你的 Arduino IDE，你可以连接你的 Arduino 并设置板和串行端口。您会看到 Arduino 上的 led 亮起。这是因为 Arduino 从 USB 获得电源。因此，当 Arduino 连接到您的电脑时，您不需要提供外部电源。接下来，您将深入一个简单的项目来演示 Arduino IDE，并学习如何构建、编译和上传基本草图。

### 项目:硬件“你好，世界！”

无处不在的“你好，世界！”Arduino 的项目是闪烁的灯。该项目使用一个 LED，一个试验板，和一些跳线。Arduino 在`loop()`迭代过程中打开和关闭。这是一个很好的开始项目，但它与如何使用传感器无关。

因此，在本节中，您将通过添加传感器来扩展闪光灯项目。在这种情况下，您仍然可以通过使用可以说是最基本的传感器来简化事情:按钮。目标是每当按钮被按下时，LED 就会亮起。

#### 硬件连接

让我们从组装 Arduino 开始。确保首先断开(关闭)Arduino。您可以使用任何带有 I/O 引脚的 Arduino 变体。在试验板上放置一个 LED 和一个按钮。将 5V 引脚连接到试验板电源轨，将接地引脚连接到接地轨，并将按钮放在试验板的中心。将 LED 放在试验板的一侧，如图 [6-19](#Fig19) 所示。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig19_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig19_HTML.jpg)

图 6-19

带按钮的 LED 图示

你就快到了。现在，将一根跳线从电源轨连接到按钮的一侧，并将按钮的另一侧连接到 Arduino 上的(`DIGITAL`)引脚 2(位于带有 USB 连接器的一侧)。接下来，将 LED 连接到试验板上的地和一个 150 欧姆的电阻(颜色:棕色、绿色、棕色、金色)。电阻的另一端应连接到 Arduino 上的第 13 号针脚。你还需要一个电阻，在按钮没按下的时候把按钮拉低。将一个 10K 欧姆电阻器(颜色:棕色、黑色、橙色、金色)放在按钮的侧面，导线连接到引脚 2 和接地。

LED 最长的一边是正极。正极应该是连接到电阻的一侧。电阻接哪个方向都没关系；它用于限制 LED 的电流。再次检查图纸，以确保您有一个类似的设置。

Note

大多数 Arduino 板都有一个连接到引脚 13 的 LED。您将重复使用该引脚来演示如何使用模拟输出。因此，您可能会看到引脚 13 附近的一个小 LED 与试验板上的 LED 同时亮起。

Cool Gadget

使用 Arduino 的最酷的小工具之一是 Adafruit ( [`www.adafruit.com/products/275`](http://www.adafruit.com/products/275) )的 Arduino 安装板。

这个小小的丙烯酸板有足够的空间放半块面包板和一个 Arduino。它甚至有安装孔，用于将 Arduino 固定在平板上，还有小橡胶脚，用于保持平板远离工作表面。下图(由 Adafruit 提供)显示了安装板的作用。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Figa_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Figa_HTML.jpg)

虽然你可以用 Lexan 或 Plexiglas(我有)制作自己的 Arduino 安装板，但 Adafruit 产品比你自己制作的产品好一点。只需大约 5.00 美元，您就可以将 Arduino 和试验板放在一起，避免在桌面上留下划痕(来自 Arduino 底部的尖头)——更好的是，避免不小心将通电的 Arduino 放在导电表面上带来的严重副作用(这绝不是一个好主意)。

#### 写素描

这个项目需要的草图在 Arduino 上使用了两个 I/O 引脚:一个输出和一个输入。输出引脚将用于点亮 LED，输入引脚将检测按钮接合。将正电压连接到按钮的一侧，另一侧连接到输入引脚。当您检测到输入引脚上的电压时，您告诉 Arduino 处理器向输出引脚发送正电压。这种情况下，LED 的正极连接到输出引脚。

如图 [6-18](#Fig18) 所示，输入引脚为 2 号引脚，输出引脚为 13 号引脚。让我们使用一个变量来存储这些数字，这样您就不必担心重复硬编码的数字(并冒着出错的风险)。使用`pinMode()`方法设置每个引脚的模式(`INPUT`、`OUTPUT`)。您将变量语句放在`setup()`方法之前，并在`setup()`方法中设置`pinMode()`调用，如下所示:

```
int led = 13;     // LED on pin 13
int button = 2;   // button on pin 2

void setup() {
  pinMode(led, OUTPUT);
  pinMode(button, INPUT);
}

```

在`loop(`方法中，您放置代码来检测按钮按压。使用`digitalRead()`方法读取管脚的状态(`LOW`或`HIGH`)，其中`LOW`表示管脚上没有电压，`HIGH`表示管脚上检测到正电压。

您还可以在`loop()`方法中放置当输入引脚状态为`HIGH`时打开 LED 的代码。在这种情况下，当输入引脚状态为`HIGH`时，使用`digitalWrite()`方法将输出引脚设置为`HIGH`，同样，当输入引脚状态为`LOW`时，将输出引脚设置为`LOW`。以下代码显示了所需的语句:

```
void loop() {
  int state = digitalRead(button);
  if (state == HIGH) {
    digitalWrite(led, HIGH);
  }
  else {
    digitalWrite(led, LOW);
  }
}

```

现在让我们看看完整的草图，并附上适当的文档。清单 [6-1](#PC3) 显示了完成的草图。

```
/*
  Simple Sensor - Beginning Sensor Networks Second Edition
  For this sketch, we explore a simple sensor (a pushbutton) and a simple response to sensor input (a LED). When the sensor
  is activated (the button is pushed), the LED is illuminated.
*/
int led = 13; // LED on pin 13
int button = 2;   // button on pin 2
// the setup routine runs once when you press reset:
void setup() {
  // initialize pin 13 as an output.
  pinMode(led, OUTPUT);
  pinMode(button, INPUT);
}
// the loop routine runs over and over again forever:
void loop() {
  // read the state of the sensor
  int state = digitalRead(button);
  // if sensor engaged (button is pressed), turn on LED
  if (state == HIGH) {
    digitalWrite(led, HIGH);
  }
  // else turn off LED
  else {
    digitalWrite(led, LOW);
  }
}

Listing 6-1Simple Sensor Sketch

```

当你已经输入了草图，你就可以编译和运行它了。将草图命名为`basic_sensor.ino`。

Tip

想要避免手动输入所有这些内容吗？你可以在 Apress 网站上找到这本书的源代码。

#### 编译和上传

写好草图后，使用 IDE 左上角的*编译*按钮测试编译。修复消息窗口中出现的任何编译错误。典型的错误包括变量或方法的拼写错误或大小写改变(编译器区分大小写)。

修复任何编译错误后，点击*上传*按钮。IDE 编译草图，并将编译后的草图上传到 Arduino 板上。您可以通过消息窗口上方右下角的进度条来跟踪进度。当编译好的草图上传后，进度条会消失。

#### 测试传感器

上传完成后，你会在 Arduino 上看到什么？如果你做对了每一件事，答案是什么都没有。它只是用一个黑暗的 LED 回望着你——几乎是嘲弄地。现在，按下按钮。LED 是否点亮？如果是这样，那么恭喜你:你是一名 Arduino 程序员！

如果 LED 不亮，按住按钮一两秒钟。如果不起作用，请检查您的所有连接，以确保您插入了试验板上的正确走线，并且您的 LED 正确就位，长边连接到电阻器，电阻器连接到引脚 13。

另一方面，如果 LED 灯一直亮着，尝试将按钮重新调整 90 度。您可能将按钮设置在了错误的方向。

尝试几次这个项目，直到兴奋过去。如果你是 Arduino 的老手，那可能是很短的一段时间。如果这一切对你来说都是新的，那就按下那个按钮，享受建造第一个传感器节点的荣耀吧！

下一节研究一个更复杂的传感器节点，使用一个发送数字数据的温度和湿度传感器。正如您将看到的，还有很多事情要做。

## 使用 Arduino 托管传感器

Arduino 的数字和模拟引脚使其成为托管传感器的理想平台。由于大多数传感器只需要很少的支持组件，所以您通常可以在一个 Arduino 上安装多个传感器。例如，可以安装一个温度传感器，甚至多个温度传感器、气压传感器、湿度传感器等等，用于从给定的地点采集天气状况。

SparkFun 和 Adafruit 有很好的网站，提供大量关于他们销售的产品的信息。通常，传感器产品页面包括示例链接和更多关于使用传感器的信息。如果你是电子产品的新手，你应该坚持使用能提供使用示例的传感器。这听起来可能像作弊，但除非你有很好的电子知识，否则不正确地使用传感器会付出很高的代价，因为你在正确使用之前已经烧坏了几个组件。

然而，当您想使用另一种传感器时，您应该查看其数据手册。大多数制造商和供应商通过产品页面上的链接提供数据手册。数据手册提供了使用传感器所需的所有信息，但可能没有实际的使用示例。如果你熟悉电子学，这是你可能需要的全部。

如果你是电子产品的业余爱好者或新手，可以查看 Arduino.cc、SparkFun 和 Adafruit 上的维基和论坛。这些站点有丰富的信息和大量的示例，并附有完整的示例代码。如果你找不到任何例子，你可以试着谷歌一下。使用类似“ *Arduino <传感器名称>示例*的术语。如果你找不到任何例子，并且不是一个有经验的电子技术人员，你可能要重新考虑使用传感器。

另一件需要考虑的事情是如何将传感器连接到 Arduino。回想一下，根据您选择的 Arduino，有许多不同的物理布局。因此，在规划 Arduino 托管的传感器节点时，您应该熟悉 Arduino 的引脚布局。如果您使用 Arduino 托管单个传感器，这可能不是问题。举例来说，图 [6-20](#Fig20) 显示了一个 Arduino Leonardo 板，突出显示了 I/O 引脚。如果你仔细观察你的 Arduino 板，你会在每个管脚旁边看到缩写的文字来表示它的用途。一些小尺寸的 Arduino 板可能没有放置标签的空间。在这种情况下，请查阅供应商的产品页面，并将其打印出来以供将来参考。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig20_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig20_HTML.jpg)

图 6-20

识别 Arduino 板上的 I/O 引脚

现在，让我们将您从学习 Arduino 中学到的知识用于构建一个带有 Arduino 和传感器的传感器节点。

## 项目:构建 Arduino 温度传感器

在这个项目中，您将构建一个更加复杂的 Arduino 托管的传感器节点。这个项目不仅演示了如何用 Arduino 托管传感器，还提供了一个例子，说明为什么需要微控制器来托管某些类型的传感器。在这种情况下，DHT22 传感器是一个数字传感器，它有自己的协议，这需要一点逻辑来正确解释，从而使它与 XBee 一起使用更加复杂。 <sup>[2](#Fn2)</sup> 稍后，您将看到一个简单模拟传感器的示例，您可以将它直接连接到 XBee 模块。

该项目使用 DHT22 温度和湿度传感器，通过试验板连接到 Arduino。DHT22 是一种产生数字信号的简单数字传感器。它需要一个电阻将数据引脚上拉至电压。这种情况下的上拉确保数据值被“上拉”到电压电平，以确保线路上的逻辑电平有效。

让我们直接进入并连接硬件。

Note

这个例子改编自 Adafruit 网站上的一个例子( [`http://learn.adafruit.com/dht`](http://learn.adafruit.com/dht) )。

### 硬件设置

本项目所需的硬件包括 Arduino、DHT22 湿度和温度传感器、试验板、4.7K 欧姆电阻(颜色:黄色、紫色、红色、金色)和试验板跳线。

Tip

如果你被卡住了或者想要更多的信息，在 Adafruit 的网站上有一个极好的教程。

首先将 Arduino 放在试验板旁边。将 DHT22 传感器插入试验板的一侧，如图 [6-21](#Fig21) 所示。请经常参考此图，并在打开 Arduino 电源(或将其连接到笔记本电脑)之前仔细检查您的连接。你要避免电混沌理论中的意外实验。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig21_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig21_HTML.jpg)

图 6-21

DHT22 接线

接下来，将 Arduino 的电源连接到试验板。用一根跳线将 Arduino 上的 5V 引脚连接到试验板电源轨，用另一根跳线将 Arduino 上的接地(GND)引脚连接到试验板的接地轨。这些电线就位后，您就可以为传感器布线了。您使用四个引脚中的三个，如表 [6-1](#Tab1) 所示。

表 6-1

DHT22 连接

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"></colgroup> 
| 

别针

 | 

连接到

 |
| --- | --- |
| one | 电源和数据引脚之间的+5V 4.7K 电阻(强上拉电阻) |
| Two | Arduino 上的引脚 7，4.7K 电阻 |
| three | 不连接 |
| four | 地面 |

接下来，将传感器的地和电源连接到试验板电源和接地轨。然后将一根电线从传感器上的数据引脚连接到 Arduino 的引脚 7。还有最后一个连接:使用一个 4.7K 欧姆的上拉电阻连接到数据线和试验板的电源轨。

### 软件设置

要将 DHT22 与 Arduino 配合使用，您需要拥有最新的 DHT22 库。您可以通过搜索库管理器直接从 Arduino IDE 安装库。打开 Arduino IDE，然后打开一个新的草图，从菜单中选择*草图* ➤ *包含库* ➤ *管理库……*。图 [6-22](#Fig22) 显示了库管理器。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig22_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig22_HTML.jpg)

图 6-22

图书馆经理

库管理器可能需要一些时间来连接到服务器并下载最新的目录。完成后，您可以在右上角的文本框中键入`DHT22`并按下`ENTER`。这将在库目录中搜索所有匹配的库。

从 Adafruit 选择 DHT 传感器库，点击`Install`。如果系统提示您安装支持库，请点击`Install all`以确保所有必备软件都已安装，如图 [6-23](#Fig23) 所示。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig23_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig23_HTML.jpg)

图 6-23

安装所有库

现在您已经配置好了硬件并设置好了 DHT22 库，让我们写一些代码吧！

### 写素描

像任何草图一样，我们需要包含一些库，定义一些常量，并在序言中实例化 DHT 对象。这里包含了`DHT`库头，将传感器的数据引脚定义为 Arduino 上的引脚 7，添加 5 秒的延迟常数，并实例化 DHT 类的一个实例。由于该库支持多种类型的 DHT 传感器，我们还必须使用库中声明的特殊类型。例如，为了告诉库使用 DHT22 传感器，我们相应地将类型设置为`DHT22`。以下是草图的序言:

```
#include "DHT.h"
#define DHTPIN 7          // DHT2 data is on pin 7
#define read_delay 5000   // 5 seconds
#define DHTTYPE DHT22     // DHT 22 (AM2302)

```

接下来，我们需要实例化 DHT 类。我们通过传递想要使用的 pin 和 DHT 类型来实现这一点。

```
DHT dht(DHTPIN, DHTTYPE);

```

DHT22 有自己的数据通信协议。幸运的是，Adafruit 的库使得从传感器读取变得容易。要读取数据，您只需调用适当的方法，如表 [6-2](#Tab2) 所示，该方法将返回值保存在变量中。

表 6-2

DHT 库中的数据方法

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"></colgroup> 
| 

方法

 | 

描述

 |
| --- | --- |
| `dht.readHumidity()` | 读取湿度 |
| `dht.readTemperature()` | 读取摄氏温度 |
| `dht.readTemperature(true)` | 读取华氏温度 |
| `dht.computeHeatIndex(temp_c, humidity, false)` | 获取以摄氏度为单位的热量指数 |
| `dht.computeHeatIndex(temp_c, humidity, true)` | 获取华氏温度的热量指数 |

知道了这些，我们需要做的就是调用这些方法来读取数据，将数据保存到变量中，然后打印出来。为了使草图简单而整洁，我们可以将这个逻辑放在一个名为`read_data()`的方法中。清单 [6-2](#PC6) 显示了完整的`read_data()`方法。

```
void read_data() {
  // Read humidity
  float humidity = dht.readHumidity();
  // Read temperature as Celsius
  float temp_c = dht.readTemperature();
  // Read temperature as Fahrenheit (isFahrenheit = true)
  float temp_f = dht.readTemperature(true);

  // Check for errors and return if any variable has no value
  if (isnan(temp_c) || isnan(temp_f) || isnan(humidity)) {
    Serial.println("ERROR: Cannot read all data from DHT-22.");
    return;
  }
  // Calculate heat index for Celsius
  float hi_c = dht.computeHeatIndex(temp_c, humidity, false);
  // Calculate heat index for temperature in Fahrenheit
  float hi_f = dht.computeHeatIndex(temp_f, humidity, true);
  Serial.print("Humidity: ");
  Serial.print(humidity);
  Serial.print("%, ");
  Serial.print(temp_c);
  Serial.print("C, ");
  Serial.print(temp_f);
  Serial.println("F ");
  Serial.print("      Heat Index: ");
  Serial.print(hi_c);
  Serial.print("C, ");
  Serial.print(hi_f);
  Serial.println("F ");
}

Listing 6-2The read_data() Method

```

请注意，代码非常简单。我们只是读取这些值，然后使用`Serial.print()`和`Serial.println()`方法将数据写入串行监视器。

唯一剩下的就是`setup()`和`loop()`方法。`setup()`方法简单地初始化了`Serial`和`dht`类。`loop()`方法使用延迟并调用`read_data()`方法。清单 [6-3](#PC7) 显示了完成的草图。

```
/*
  Beginning Sensor Networks Second Edition
  Sensor Networks Example Arduino Hosted Sensor Node
  This sensor node uses a DHT22 sensor to read temperature and humidity printing the results in the serial monitor.
*/
#include "DHT.h"

#define DHTPIN 7          // DHT2 data is on pin 7
#define read_delay 5000   // 5 seconds
#define DHTTYPE DHT22     // DHT 22 (AM2302)

DHT dht(DHTPIN, DHTTYPE);

void read_data() {
  // Read humidity
  float humidity = dht.readHumidity();
  // Read temperature as Celsius
  float temp_c = dht.readTemperature();
  // Read temperature as Fahrenheit (isFahrenheit = true)
  float temp_f = dht.readTemperature(true);

  // Check for errors and return if any variable has no value
  if (isnan(temp_c) || isnan(temp_f) || isnan(humidity)) {
    Serial.println("ERROR: Cannot read all data from DHT-22.");
    return;
  }
  // Calculate heat index for Celsius
  float hi_c = dht.computeHeatIndex(temp_c, humidity, false);
  // Calculate heat index for temperature in Fahrenheit
  float hi_f = dht.computeHeatIndex(temp_f, humidity, true);
  Serial.print("Humidity: ");
  Serial.print(humidity);
  Serial.print("%, ");
  Serial.print(temp_c);
  Serial.print("C, ");
  Serial.print(temp_f);
  Serial.println("F ");
  Serial.print("      Heat Index: ");
  Serial.print(hi_c);
  Serial.print("C, ");
  Serial.print(hi_f);
  Serial.println("F ")

;
}

void setup() {
  Serial.begin(115200);  // Set the serial port speed
  dht.begin();
  delay(1000);
  Serial.println("Welcome to the DHT-22 Arduino example!\n");
}

void loop() {
  delay(read_delay);
  read_data();
}

Listing 6-3Completed Sketch: Reading a DHT-22 Sensor

```

如果您尚未这样做，请通过单击*新菜单*按钮或选择*文件* ➤ *新*来打开一个新的 Arduino 草图。现在您可以编译、上传和测试项目了。你可以给它起任何你喜欢的名字，比如`dht22_example.ino`。

### 测试执行

执行草图意味着将它上传到你的 Arduino 并观看它运行。如果您尚未连接 Arduino，现在就可以连接。

我喜欢从画草图开始。单击 Arduino 应用程序左侧的复选标记，观察底部信息屏幕中的输出。如果您看到错误，请修复它们并重试编译。常见错误包括丢失 DHT22 库(这可能需要重新启动 Arduino 应用程序)、键入错误、语法错误等。一旦一切编译正确，你就可以点击工具栏上的*上传*按钮来上传你的草图了。

上传完成后，单击工具栏右侧的按钮打开串行监视器。观察 Arduino 消息。清单 [6-4](#PC8) 显示了您应该看到的典型输出。

```
Welcome to the DHT-22 Arduino example!

Humidity: 48.00%, 18.20C, 64.76F
      Heat Index: 17.33C, 63.19F
Humidity: 50.00%, 18.30C, 64.94F
      Heat Index: 17.49C, 63.48F
Humidity: 51.80%, 19.10C, 66.38F
      Heat Index: 18.42C, 65.15F
Humidity: 53.60%, 20.20C, 68.36F
      Heat Index: 19.67C, 67.42F
Humidity: 53.20%, 21.40C, 70.52F
      Heat Index: 20.98C, 69.77F
Humidity: 51.50%, 22.10C, 71.78F
      Heat Index: 21.71C, 71.08F
Humidity: 50.00%, 22.50C, 72.50F
      Heat Index: 22.11C, 71.80F
Humidity: 48.50%, 22.60C, 72.68F
      Heat Index: 22.18C, 71.93F

Listing 6-4Output of the DHT22 Sensor Sketch

```

如果你看到类似的输出，恭喜你！您刚刚构建了第一个 Arduino 托管的传感器节点。这是构建传感器网络的重要一步，因为您现在已经拥有了开始构建更复杂的无线传感器节点和用于记录传感器数据的聚合节点所需的工具。

让我们进一步体验 Arduino 传感器，添加 XBee 模块，使传感器能够远离 Arduino 放置。这有效地展示了 Arduino 如何远程托管多个传感器节点，从而成为传感器网络中的聚合节点。

## 项目:使用 Arduino 作为 XBee 传感器节点的数据收集器

这个项目结合了你在本章学到的 Arduino 和在第 [2](02.html) 和 [4](04.html) 章学到的 XBee。更具体地说，您使用一个 Arduino 和一个使用 XBee 模块将传感器与 Arduino 连接起来的远程传感器。我们将重用第四章[中的 XBee 传感器节点，并使用 Arduino 读取数据。](04.html)

### XBee 传感器节点

按照第 [4](04.html) 章的内容创建 XBee 传感器节点。提醒一下，该节点的构造如图 [6-24](#Fig24) 所示。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig24_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig24_HTML.jpg)

图 6-24

XBee 传感器节点

如果您尚未配置第 [4](04.html) 章中的传感器节点，或者如果您需要重置模块，您应该首先确保加载了最新的固件，并使用表 [6-3](#Tab3) 中所示的设置。请注意，您不需要第 [4](04.html) 章中的 *IR* 设置，但是如果您想要重用您在该章中使用的模块，这是可以的。

表 6-3

XBee 传感器节点选项和值

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"> <col class="tcol3 align-left"> <col class="tcol4 align-left"></colgroup> 
| 

密码

 | 

设置名称

 | 

描述

 | 

价值

 |
| --- | --- | --- | --- |
| D3 | a3/上帝 3 | 触发模拟或数字数据记录 | 2—ADC |
| 身份 | 平移 ID | 网络的 Id | Eight thousand and eighty-eight |
| 红外的 | 输入输出采样率 | 等待发送数据的时间 | 3a 98—15000 毫秒 |
| 镍 | 节点标识符 | 节点的名称 | TMP36 |
| V+ | 电源电压阈值 | 电源电压 | FFFF(总是发送) |

注意，与第 [5](05.html) 章中的项目不同，我们必须设置 I/O 采样率。这是因为我们将使用的库不具备在 ZigBee 网络中搜索远程节点的能力。相反，在这个项目中，Arduino 将进行轮询，直到 IO 样本被发送到协调器节点。因此，我们已经看到了从 ZigBee 网络获取数据的两种不同方式——直接从节点请求数据(第 [5](05.html) 章)和轮询从节点发送的数据(本章)。

### 协调节点

协调器节点应使用最新加载的固件和表 [6-4](#Tab4) 中所示的设置进行类似配置。

表 6-4

XBee 协调器选项和值

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"> <col class="tcol3 align-left"> <col class="tcol4 align-left"></colgroup> 
| 

密码

 | 

设置名称

 | 

描述

 | 

价值

 |
| --- | --- | --- | --- |
| 身份 | 平移 ID | 网络的 Id | Eight thousand and eighty-eight |
| 镍 | 节点标识符 | 节点的名称 | 协调者 |

现在还不需要安装 XBee 模块。您需要配置其设置。您将在下一节中执行该操作。

现在，让我们设置 Arduino 和 XBee。

### 带 XBee 盾的 Arduino

您可以使用 Arduino 从 XBee 传感器节点读取数据。这是一个使用 Arduino 作为来自 XBee 传感器节点的传感器数据的数据聚合器(收集器)的例子。让我们用 XBee 设置一个 Arduino。这个项目演示了使用 Arduino 通过 XBee 接收数据，但是你也可以通过 XBee 发送数据。

#### 硬件设置

本节中的示例设置使用典型的 Arduino (Uno、Leonardo 等。)支持标准屏蔽。虽然没有明确要求必须使用设计用于接受 XBee 模块的屏蔽，但是大多数 XBee 屏蔽都是为了使 XBee 的使用更容易。换句话说，你不必担心如何将 XBee 连接到 Arduino。图 [6-25](#Fig25) 显示了一个来自 SparkFun 的带有 XBee 盾的 Arduino。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig25_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig25_HTML.jpg)

图 6-25

Arduino XBee 盾(SparkFun 提供)

我使用这个屏蔽来演示如何与 XBee 模块通信。如果您决定使用另一个 shield，请务必查看该 shield 的文档，查看如何使用它的示例，并将其与本项目中的代码进行比较。进行适当的修改(硬件连接和对草图的更改),以便您的项目可以正确地与您的盾一起工作。

shield 允许您选择通过数字引脚 0 和 1 与 Arduino 的板载串行电路(UART <sup>[3](#Fn3)</sup> )进行通信。但这些也是从 Arduino IDE 通过 USB 与 Arduino 通信时使用的引脚。幸运的是，SparkFun XBee shield 有一个小开关，允许你选择使用引脚 2 和 3 来代替。使用此选项，您可以编写一个脚本，通过 XBee 从 shield 读取数据，同时仍然连接到 Arduino IDE 并使用串行监视器。但是有一个问题:只有一个 UART 可用。您必须使用软件串行库来模拟第二个串行连接。Arduino IDE 中包含软件串行库。您可以在“软件设置”一节中看到如何操作。

Tip

如果您使用不同的 XBee 屏蔽，您应该参考屏蔽上的文档，并按照说明使用引脚。有些屏蔽是硬连线的。

如果您不想使用屏蔽，您可以像前面一样将 XBee 连接到 Arduino。在本例中，您使用 SparkFun 的 XBee 分线板来连接试验板。图 [6-26](#Fig26) 显示了将 XBee Explorer 调节分线板连接到 Arduino 的接线图。请注意，您使用的是 Arduino 的 5V 引脚。如果您使用的是非调节分线板，您应该使用 3.3V 引脚。在给项目通电之前，一定要仔细检查您使用的任何组件的最大电压。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig26_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig26_HTML.jpg)

图 6-26

通过 SparkFun XBee 分线板将 XBee 连接到 Arduino

Note

这两种方法都适用于这个项目。

无论您选择哪种方法，请将 XBee 协调器模块从 USB 适配器上取下，并将其插入 XBee shield 或 XBee Explorer 调节分线板。现在硬件已经准备好了，让我们设置您的 Arduino 环境，并编写一个草图来从 XBee 传感器节点读取数据。

#### 软件设置

像大多数传感器一样，我们需要一个库来连接和读取数据。幸运的是，Arduino 有一个很好的 XBee 串行库。我们像安装 DHT22 库一样，打开一个新的草图，从菜单中选择*草图* ➤ *包含库* ➤ *管理库…* ，然后搜索 XBee，从 Andrew Rapp 安装库。图 [6-27](#Fig27) 显示了选择了正确库的库管理器。点击*安装*进行安装。

![../images/313992_2_En_6_Chapter/313992_2_En_6_Fig27_HTML.jpg](../images/313992_2_En_6_Chapter/313992_2_En_6_Fig27_HTML.jpg)

图 6-27

加载 XBee Arduino 库

一旦安装了库并且重启了 Arduino IDE，就可以编写脚本从 XBee 中读取数据。该库为每个流行的 XBee 数据包提供了类，用于向 XBee 发送数据或从 XBee 接收数据。这个项目使用 IO sample 类，因为您知道这是我们在这个项目中唯一感兴趣的包。

你需要创建草图的几个部分。使用 XBee 库比编写自己的通信方法更容易，但是这个库有一些设置步骤和方法，您需要使用它们来读取数据包。

首先，让我们包括 XBee 和软件串行库的库头。回想一下，软件串行库是 Arduino IDE 的一部分:

```
#include <XBee.h>
#include <SoftwareSerial.h>

```

现在，您必须定义用于与 XBee 模块通信的引脚。您将串行监视器用作输出设备，因此需要使用替代引脚。在这种情况下，使用引脚 2 和 3 进行接收和发射连接。您需要定义这些并初始化软件串行库，并使用它与 XBee 通信。下面显示了所需的定义:

```
uint8_t recv = 8;
uint8_t trans = 9;
SoftwareSerial soft_serial(recv, trans);

```

接下来，您必须实例化 XBee 库和助手类。在这种情况下，您需要 I/O 数据示例包的 helper 类:

```
XBee xbee = XBee();
ZBRxIoSampleResponse ioSample = ZBRxIoSampleResponse();

```

现在我们准备写启动代码。对于这个项目，您必须启动软件串行库，并将其传递给 XBee 库，以便在与 XBee 模块通信时使用。您还需要初始化默认的 serial 类，这样您就可以使用`print()`语句在代码的后面部分显示读取的数据。下面显示了完整的`setup()`方法:

```
void setup() {
  Serial.begin(9600);
  while (!Serial);   // Leonardo boards need to wait for Serial to start
  soft_serial.begin(9600);
  xbee.setSerial(soft_serial);
}

```

注意带有 while 循环的那一行。你需要把这个加到达芬奇的画板上。如果你忽略了这一点，并在莱昂纳多板上运行草图，XBee 可能无法工作。添加此循环以允许 Leonardo 有时间启动串行实例。

现在，让我们编写用于从数据包中读取数据的方法。稍后您将学习如何从 XBee 读取数据包。首先，让我们看看如何获得数据包的源地址。下面显示了这样做的代码:

```
void get_address(ZBRxIoSampleResponse *ioSample) {
  Serial.print("Received data from address: ");
  Serial.print(ioSample->getRemoteAddress64().getMsb(), HEX);
  Serial.print(ioSample->getRemoteAddress64().getLsb(), HEX);
  Serial.println("");
}

```

注意，您只需使用`ioSample`类实例并调用方法`getRemoteAddress64().getMsb()`。实际上，这是对一个子类(RemoteAddress64)及其方法`getMsb()`的调用。这将返回 64 位地址的最高有效字节(高 16 位)。对于`getRemoteAddress64().getLsb()`调用，您对最低有效位做同样的操作。然后打印这些值，指定要以十六进制打印它们。如果您从多个 XBee 节点读取数据，为每个地址应用一个名称会很方便，比如“卧室”或“起居室”。我把这个留给你做练习。

接下来，您想要读取数据有效负载。在这种情况下，您想要读取从 XBee 传感器节点发送到 XBee 协调器的温度数据。下面显示了执行此操作所需的代码。您可以使用前面讨论的公式将传感器读取的毫伏值转换为摄氏温度，然后再转换为华氏温度。

```
void get_temperature(ZBRxIoSampleResponse *ioSample) {
  float adc_data = ioSample->getAnalog(3);
  Serial.print("Temperature is ");
  float temperatureC = ((adc_data * 1200.0 / 1024.0) - 500.0) / 10.0;
  Serial.print(temperatureC);
  Serial.print("c, ");
  float temperatureF = ((temperatureC * 9.0)/5.0) + 32.0;
  Serial.print(temperatureF);
  Serial.println("f");
}

```

最后，您需要从数据包中读取电源电压。这种情况下，电源电压出现在数据采样之后。因为您知道只有一个数据样本(通过模拟样本掩码)，所以您知道模拟电压正好出现在校验和之前。遗憾是，目前还没有从 XBee 库中的 I/O sample 包获取信息的方法。但是，并没有全部丢失，因为库的作者将数据存储在一个数组中，并提供了一个子类供您用来获取原始数据。在这种情况下，您需要数据中的字节 17(最高有效字节)和 18(最低有效字节)。你知道这些是从零开始从帧类型之后的字节计数所需的索引。详见表 [6-5](#Tab5) 。

与温度数据一样，您必须使用前面讨论的公式将读数转换为伏特。下面显示了读取、转换和显示 XBee 传感器节点的电源电压所需的代码。请注意，您将最高有效字节移动了 8 位，以便可以保留 16 字节的浮点值。

```
void get_supply_voltage() {
  Serial.print("Supply voltage is ");
  int ref = xbee.getResponse().getFrameData()[17] << 8;
  ref += xbee.getResponse().getFrameData()[18];
  float volts = (float(ref) * float(1200.0 / 1024.0))/1000.0;
  Serial.print(" = ");
  Serial.print(volts);
  Serial.println(" volts.");
}

```

花些时间检查一下计算结果。在本例中，您将 XBee 传感器节点读取和发送的电压转换为摄氏度，然后再转换为华氏度。为了便于阅读，您还将电源电压转换为伏特。测试期间，所有这些值都被发送到串行监视器进行反馈。

一旦实现了这些方法，就可以将从 XBee 读取数据的代码放在`loop()`方法中，调用这些方法来解密数据并将其打印到串行监视器上。

因为这个`loop()`方法被重复调用，所以您使用 XBee 类方法来读取包，然后确定包是否是 I/O 数据样本包。如果是，就从包中读取数据。如果不是，您添加一些简单的错误处理，以便 Arduino 可以继续读取数据而不是停止。下图显示了完成的`loop()`方法:

```
void loop() {
  xbee.readPacket();
  if (xbee.getResponse().isAvailable()) {
    if (xbee.getResponse().getApiId() == ZB_IO_SAMPLE_RESPONSE) {
      xbee.getResponse().getZBRxIoSampleResponse(ioSample);
      // Read and display data
      get_address(&ioSample);
      get_temperature(&ioSample);
      get_supply_voltage();
    }
    else {
      Serial.print("Expected I/O Sample, but got ");
      Serial.print(xbee.getResponse().getApiId(), HEX);
    }
  } else if (xbee.getResponse().isError()) {
    Serial.print("Error reading packet.  Error code: ");
    Serial.println(xbee.getResponse().getErrorCode());
  }
}

```

请注意，在代码中，您检查数据包是否可用；如果是的话，你读一下。如果读取的数据包是正确的帧类型，在本例中为`ZB_IO_SAMPLE_RESPONSE`，则从数据包中读取数据并显示。如果它不是正确的包，你把收到的包的帧类型打印到串行监视器上。如果读取数据包时出现错误，您可以在最后一个 else 中捕获该错误，并在串行监视器上显示该错误。

注意`ZB_IO_SAMPLE_RESPONSE`条件代码块的内容。首先用读取的数据初始化 I/O 数据样本类，然后读取发送数据包的 XBee 的地址，然后计算温度和参考电压。

到目前为止，一旦你理解了代码，创建一个新的文件，并在新的草图窗口中输入信息。清单 [6-5](#PC17) 显示了 Arduino XBee 接收器项目的完整草图。该代码也可以在本书的源代码链接中的 Apress 站点上获得。

```
/**
  Beginning Sensor Networks Second Edition
  Sensor Networks Example Arduino Receiver Node

  This project demonstrates how to receive sensor data from
  an XBee sensor node. It uses an Arduino with an XBee shield
  with an XBee coordinator installed.

  Note: This sketch was adapted from the examples in the XBee
  library created by Andrew Rapp.
*/

#include <XBee.h>
#include <SoftwareSerial.h>

// Setup pin definitions for XBee shield
uint8_t recv = 2;
uint8_t trans = 3;
SoftwareSerial soft_serial(recv, trans);

// Instantiate an instance of the XBee library
XBee xbee = XBee();

// Instantiate an instance of the IO sample class
ZBRxIoSampleResponse ioSample = ZBRxIoSampleResponse();

void setup() {
  Serial.begin(9600);
  while (!Serial);   // Leonardo boards need to wait for Serial to start
  soft_serial.begin(9600);
  xbee.setSerial(soft_serial);
  Serial.println("Hello. Welcome to the Arduino XBee Data Aggregator.");
}

// Get address and print it

void get_address(ZBRxIoSampleResponse *ioSample) {
  Serial.print("Received data from address: ");
  Serial.print(ioSample->getRemoteAddress64().getMsb(), HEX);
  Serial.print(ioSample->getRemoteAddress64().getLsb(), HEX);
  Serial.println("");
}

// Get temperature and print it
void get_temperature(ZBRxIoSampleResponse *ioSample) {
  float adc_data = ioSample->getAnalog(3);

  Serial.print("Temperature is ");
  float temperatureC = ((adc_data * 1200.0 / 1024.0) - 500.0) / 10.0;
  Serial.print(temperatureC);
  Serial.print("c, ");
  float temperatureF = ((temperatureC * 9.0)/5.0) + 32.0;
  Serial.print(temperatureF);
  Serial.println("f");
}

// Get supply voltage and print it
void get_supply_voltage() {
  Serial.print("Supply voltage is ");
  int ref = xbee.getResponse().getFrameData()[17] << 8;
  ref += xbee.getResponse().getFrameData()[18];
  float volts = (float(ref) * float(1200.0 / 1024.0))/1000.0;
  Serial.print(" = ");
  Serial.print(volts);
  Serial.println(" volts.");
}

void loop() {
  //attempt to read a packet
  xbee.readPacket();

  if (xbee.getResponse().isAvailable()) {
    // got something

    if (xbee.getResponse().getApiId() == ZB_IO_SAMPLE_RESPONSE) {

      // Get the packet
      xbee.getResponse().getZBRxIoSampleResponse(ioSample);

      // Read and display data
      get_address(&ioSample);
      get_temperature(&ioSample);
      get_supply_voltage();
    }
    else {
      Serial.print("Expected I/O Sample, but got ");
      Serial.print(xbee.getResponse().getApiId(), HEX);
    }
  } else if (xbee.getResponse().isError()) {
    Serial.print("Error reading packet.  Error code: ");
    Serial.println(xbee.getResponse().getErrorCode());
  }
}

Listing 6-5Arduino XBee Receiver

```

在将草图上传到 Arduino 之前，请花些时间确保它已经编译好。记住，草图一旦上传，就开始运行了。另存为`xbee_sensor.ino`。

### 测试最终项目

要测试该项目，请确保首先启动 Arduino，然后启动 XBee 传感器节点。启动 Arduino，上传草图，然后打开串行监视器。当 XBee 节点被 Arduino 上的协调器接受并添加到网络中时，您应该会观察到 XBee 调节分线板上的链路灯闪烁。在大约 5 秒钟内，XBee 传感器节点开始发送数据。发生这种情况时，Arduino 草图应该开始将语句打印到您的串行监视器。清单 [6-6](#PC18) 显示了您应该在串行监视器中看到的输出示例。

```
Hello. Welcome to the Arduino XBee Data Aggregator.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.83 volts.
Received data from address: 13A2004192DB79
Temperature is 11.76c, 53.16f
Supply voltage is  = 3.83 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.82 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.83 volts.
Received data from address: 13A2004192DB79
Temperature is 12.34c, 54.22f
Supply voltage is  = 3.83 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.82 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.82 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.82 volts.
Received data from address: 13A2004192DB79
Temperature is 12.46c, 54.43f
Supply voltage is  = 3.82 volts.

Listing 6-6Sample Output of the XBee Arduino Sketch

```

你看到类似的东西了吗？如果是这样，那么您已经做了大量的工作，现在已经拥有了构建传感器节点和基于 Arduino 的传感器数据聚合器的基本组件。

如果您在串行监视器中看不到任何输出，请不要惊慌。相反，请仔细检查 Arduino 上的 XBee 是否正确插入，以及您是否使用了草图中与您正在使用的 XBee shield 如何连接到 Arduino 相对应的正确引脚(并非所有 shield 都像 SparkFun shield 一样使用引脚 2 和 3)。提示:查看你的盾的文档。

如果所有这些都是正确的，请确保在连接到 Arduino 的 XBee 上使用协调器 API 固件，在 XBee 传感器节点上使用路由器 API 固件。如果仍有问题，请返回到上一个项目，以确保传感器节点仍在工作。

您也可以尝试关闭 Arduino 和 XBee 传感器节点；然后打开 Arduino，等待大约 10 秒钟，并再次打开 XBee 传感器节点。有时握手过程和网络加入可能会停止，一段时间内什么都不会发生。按此顺序关闭再打开 XBee 可确保它会重新尝试配置。

另一方面，也许你正在获得数据，但它是不正确的——温度读数对于实际环境来说太低了。我曾经遇到过这种情况，当时我用来连接 TMP36 上数据引脚的电线被意外拔下。底线是总是检查，再检查你的线路。

### 为了更多乐趣

如果您想要扩展项目，您可以添加第二个 XBee 传感器节点，并修改 Arduino 草图以提供每个节点的位置。例如，您可以将一个节点标记为“办公室”，将另一个节点标记为“厨房”。草图应记录(写入串行监视器)传感器的位置以及来自 XBee 的传感器数据。

## 部件购物清单

完成本章中的项目需要一些组件。它们在表 [6-5](#Tab5) 中列出。

表 6-5

所需组件

<colgroup><col class="tcol1 align-left"> <col class="tcol2 align-left"> <col class="tcol3 align-left"> <col class="tcol4 align-left"></colgroup> 
| 

项目

 | 

供应商

 | 

是吗？成本美元

 | 

所需数量

 |
| --- | --- | --- | --- |
| 发光二极管(任何颜色) | [T2`www.sparkfun.com/products/9592`](http://www.sparkfun.com/products/9592) | $0.35 | one |
| 按钮(试验板安装) | [T2`www.sparkfun.com/products/97`](http://www.sparkfun.com/products/97) | $0.35 | one |
| 试验板(非迷你) | [T2`www.sparkfun.com/products/9567`](http://www.sparkfun.com/products/9567) | $5.95 | one |
| 试验板跳线 | [T2`www.sparkfun.com/products/8431`](http://www.sparkfun.com/products/8431) | $3.95 | one |
| [T2`www.adafruit.com/product/758`](http://www.adafruit.com/product/758) |
| DHT22 | [T2`www.sparkfun.com/products/10167`](http://www.sparkfun.com/products/10167) | $9.95 | one |
| [T2`www.adafruit.com/products/385`](http://www.adafruit.com/products/385) |
| 150 欧姆电阻器 | 大多数在线和零售商店 | 变化 | one |
| 4.7K 欧姆电阻器 | 大多数在线和零售商店 | 变化 | one |
| 10K 欧姆电阻器 | 大多数在线和零售商店 | 变化 | one |
| Arduino XBee 神盾局 | [T2`www.sparkfun.com/products/10854`](http://www.sparkfun.com/products/10854) | $24.95 | one |
| XBee-ZB (ZB)系列 2、2.5 或 3 | [T2`www.sparkfun.com`](http://www.sparkfun.com) | $25.00+ | Two |
| [T2`www.adafruit.com`](http://www.adafruit.com) |
| [T2`www.makershed.com`](http://www.makershed.com) |
| TMP36 传感器 | [T2`www.sparkfun.com/products/10988`](http://www.sparkfun.com/products/10988) | $1.50 | one |
| [T2`www.adafruit.com/products/165`](http://www.adafruit.com/products/165) |
| 试验板电源 | [T2`www.sparkfun.com/products/10804`](http://www.sparkfun.com/products/10804) | $14.95 | one |
| 壁式电源(6V–12V) | [T2`www.sparkfun.com/products/15314`](http://www.sparkfun.com/products/15314) | $5.95 | one |
| 0.10 毫伏电容 | [T2`www.sparkfun.com/products/8375`](http://www.sparkfun.com/products/8375) | $0.25 | one |
| XBee Explorer 由集管调节 | [T2`www.sparkfun.com/products/11373`](http://www.sparkfun.com/products/11373) | $9.95 | one |
| 分离式公接头(可选) | [T2`www.adafruit.com/products/392`](http://www.adafruit.com/products/392) | $4.95 | one |
| Arduino Uno(任何支持盾牌的) | 各种各样的 | 25 美元及以上 | one |
| 斯帕克芬 XBee 盾 | [T2`www.sparkfun.com/products/10854`](http://www.sparkfun.com/products/10854) | $24.95 | one |
| 烙铁和焊料(可选) | 大多数在线和零售商店 | 变化 | one |

## 摘要

这一章涵盖了很多内容。您探索了 Arduino 平台，包括许多可用的表单以及如何编写草图(程序)来控制 Arduino。我还向您展示了如何使用温度和湿度传感器在 Arduino 中托管传感器。

您应用了在第 [2](02.html) 和 [4](04.html) 章中学到的关于 XBee 的信息，创建了一个 XBee 传感器节点来读取温度数据。然后，使用 XBee 协调器设置一个 Arduino，从 XBee 传感器节点接收传感器数据，并将其显示在串行监视器中。

在下一章中，您将发现各种用于存储传感器数据的机制，无论是板载的还是云中的。

<aside aria-label="Footnotes" class="FootnoteSection" epub:type="footnotes">Footnotes [1](#Fn1_source)

[T2`https://en.wikipedia.org/wiki/Pulse-width_modulation`](https://en.wikipedia.org/wiki/Pulse-width_modulation)

  [2](#Fn2_source)

至少，我还没有发现有人成功做到了这一点。

  [3](#Fn3_source)

[T2`http://en.wikipedia.org/wiki/Universal_asynchronous_receiver/transmitter`](http://en.wikipedia.org/wiki/Universal_asynchronous_receiver/transmitter)

 </aside>